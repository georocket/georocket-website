plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
    id "com.moowork.gulp" version "1.1.1"
    id "com.moowork.node" version "1.1.1"
}

apply plugin: 'org.asciidoctor.convert'

import org.apache.tools.ant.filters.ConcatFilter

ext {
    docsSources = new File(buildDir, "docs-sources")
    docs = new File(buildDir, "docs")
    src = new File("src")
    srcGen = new File(buildDir, "src-gen")
    userDocsDest = new File(src, "docs/user-documentation")
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

configurations {
    docs_1_0_0 {
        transitive = false
    }

    docs_1_1_0 {
        transitive = false
    }
}

dependencies {
    // 1.0.0
    docs_1_0_0 "io.georocket:georocket-docs:1.0.0:sources"
    docs_1_0_0 "io.georocket:georocket-client-api:1.0.0:javadoc"
    docs_1_0_0 "io.georocket:georocket-server-api:1.0.0:javadoc"

    // 1.1.0
    docs_1_1_0 "io.georocket:georocket-docs:1.1.0-SNAPSHOT:sources"
    docs_1_1_0 "io.georocket:georocket-client-api:1.1.0-SNAPSHOT:javadoc"
    docs_1_1_0 "io.georocket:georocket-server-api:1.1.0-SNAPSHOT:javadoc"
}

gulp {
    colors = true
}

node {
    version = '7.4.0'
    download = true
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

task extractDocs << {
    // extract each 'docs' artifact to a separate directory
    [configurations.docs_1_0_0, configurations.docs_1_1_0].each { cfg ->
        cfg.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            copy {
                from zipTree(artifact.file)
                into new File(new File(docsSources, artifact.name), artifact.moduleVersion.id.version)
            }
        }
    }
}

task copyDocs(dependsOn: [extractDocs, asciidoctor]) << {
    // copy latest version of user documentation
    copy {
        from new File(asciidoctor.outputDir, "html5/1.0.0")
        into new File(srcGen, "georocket-docs-html")
    }

    // copy all versions of user documentation
    copy {
        from new File(asciidoctor.outputDir, "html5")
        into new File(srcGen, "georocket-docs-html")
    }

    // copy client API
    copy {
        from new File(docsSources, "georocket-client-api")
        into new File(docs, "api/client")
    }

    // copy server API
    copy {
        from new File(docsSources, "georocket-server-api")
        into new File(docs, "api/server")
    }
}

task prepareUserDocs(dependsOn: [copyDocs]) << {
    copy {
        from new File(srcGen, "georocket-docs-html")
        into userDocsDest
    }
}

task cleanUserDocs(type: Delete) {
    delete userDocsDest
}

asciidoctor {
    sourceDir new File(docsSources, "georocket-docs")
    options template_dirs: ["${projectDir}/asciidoctor-backends"]

    resources {
        from(sourceDir) {
            include "**/images/*"
        }
    }

    // disable idprefix and set id separator so that the Bootstrap scrollspy
    // plugin correctly finds headings
    attributes "idprefix": "", "idseparator": "-"
}

asciidoctor.dependsOn(extractDocs)

gulp_build.dependsOn copyDocs, prepareUserDocs, yarn_install

assemble.dependsOn gulp_build
clean.dependsOn cleanUserDocs

defaultTasks "build"
